plugins {
    id 'java'
    id 'xyz.wagyourtail.unimined' // version '1.0.0'
}

group 'com.example'
archivesBaseName 'UniminedExampleMod'
version '1.0-SNAPSHOT'

// this is just here so we can test the outputs easier and clean between tests
unimined.useGlobalCache = false

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    fabric {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    forge {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    maven {
        name = "wagyourtail releases"
        url = "https://maven.wagyourtail.xyz/releases"
    }
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    mavenCentral()
}

unimined.minecraft {
    version project.minecraft_version

    mappings {
        searge()
        mcp("stable", "22-1.8.9")
    }

    defaultRemapJar = false
}

unimined.minecraft(sourceSets.fabric) {
    version project.minecraft_version

    mappings {
        searge()
        mcp("stable", "22-1.8.9")
    }

    legacyFabric {
        loader project.fabric_version
    }
}

unimined.minecraft(sourceSets.forge) {
    version project.minecraft_version

    mappings {
        mcp("stable", "22-1.8.9")
    }

    minecraftForge {
        loader project.forge_version
        mixinConfig "modid.mixins.json"
    }
}

configurations {
    mainImplementation
    implementation {
        extendsFrom forgeImplementation
        extendsFrom fabricImplementation
    }
}

sourceSets {
    main {
        compileClasspath += configurations.mainImplementation
        runtimeClasspath += configurations.mainImplementation
    }
}

dependencies {
    // we need this in main where it isn't by default
    mainImplementation "org.spongepowered:mixin:0.8.5-SNAPSHOT"
}

jar {
    enabled = false
}

processFabricResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processForgeResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }

    filesMatching("mcmod.info") {
        expand "version": project.version
    }
}